(ns cmr.umm.dif.collection.location-keywords
  "Provide functions to parse and generate DIF location / spatial keyword elements."
  (:require [clojure.data.xml :as x]
            [cmr.common.xml :as cx]
            [cmr.umm.collection :as c]
            [cmr.umm.dif.core :as dif]
            [cmr.umm.generator-util :as gu]
            [cmr.common-app.services.kms-fetcher :as kf]
            [clojure.string :as str]
            [clojure.set :as set]))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; The following functions are all copied directly from the cmr.umm-spec.location-keywords namespace
(def duplicate-keywords
  "Lookup table to account for any duplicate keywords. Will choose the preferred value.
  Common key is :uuid which is a field in the location-keyword map. "
   ;; Choose Black Sea here because it's more associated with Eastern Europe than Western Asia.
  {"BLACK SEA" {:uuid "afbc0a01-742e-49da-939e-3eaa3cf431b0"}
   ;; Choose a more specific SPACE element because the general SPACE is too broad and top-level.
   "SPACE" {:uuid "6f2c3b1f-acae-4af0-a759-f0d57ccfc83f"}
   ;; Choose Georgia the country instead of Georgia the US State.
   "GEORGIA" {:uuid "d79e134c-a4d0-44f2-9706-cad2b59de992"}})

(def location-keyword-order
  "Defines the order of hierarchical keywords for LocationKeywords"
  [:Category :Type :Subregion1 :Subregion2 :Subregion3])

(def cache-location-keywords->umm-location-keywords
  "Mapping for renaming values generated by the kms fetcher to LocationKeyword UMM keys"
  {:category :Category
   :type :Type
   :subregion-1 :Subregion1
   :subregion-2 :Subregion2
   :subregion-3 :Subregion3
   :detailed-location :DetailedLocation})

(defn get-spatial-keywords-maps
  "Takes a context map and returns a list of maps that correspond to the spatial keywords hierarchy of the full map of GCMD keywords.
    Keys are :category :type :subregion-1 :subregion-2 :subregion-3 :uuid. Values are single string-type.
    Not all keys are present in every map."
  [context]
  (:spatial-keywords (kf/get-gcmd-keywords-map context)))

(defn find-spatial-keywords-in-map
  "Finds all occurrences of a spatial keyword in the spatial-keywords map.
  Takes a string keyword as a parameter, e.g. 'OCEAN' and a list of spatial keyword maps;
  returns a list of maps of hierarchies which contain the keyword."
  [keyword-map-list keyword]
  (filter (fn [map]
            (some #{(str/upper-case keyword)} (mapv str/upper-case (vals map))))
          (vals keyword-map-list)))

(defn find-spatial-keyword
  "Finds spatial keywords in the hierarchy and pick the one with the fewest keys (e.g. shortest
  hierarchical depth.) Takes a string keyword as a parameter, a list of keyword maps and returns
  the map of hierarichies which contain the keyword (treated case insensitive).
  You can also pass :uuid as a keyword argument e.g. 'afbc0a01-742e-49da-939e-3eaa3cf431b0' for
  'BLACK SEA'. If the keyword is a duplicate, it will substitute the correct one."
  [keyword-map-list keyword]
  (if (contains? duplicate-keywords keyword)
    (first (find-spatial-keywords-in-map keyword-map-list (:uuid (get duplicate-keywords (str/upper-case keyword)))))
    (let [result (first (sort-by count (find-spatial-keywords-in-map keyword-map-list keyword)))]
      (or result {:Category "OTHER" :Type keyword}))))

(defn spatial-keywords->location-keywords
  "Takes a keyword map list and a list of Spatial Keywords and returns a list of location keyword maps
  for that context"
  [keyword-map-list spatial-keywords]
  (map (fn [keyword]
         (dissoc
          (set/rename-keys
            (find-spatial-keyword keyword-map-list keyword)
            cache-location-keywords->umm-location-keywords)
          :uuid))
       spatial-keywords))

(defn location-values
  "Returns the location keyword values in order so that we can get the last one"
  [location-keyword]
  (for [k location-keyword-order
        :let [value (get location-keyword k)]
        :when value]
    value))

(defn leaf-value
  "Returns the leaf value of the location-keyword object to be put in a SpatialKeywords list"
  [location-keyword]
  (last (location-values location-keyword)))

(defn location-keywords->spatial-keywords
  "Converts a list of LocationKeyword maps to a list of SpatialKeywords"
  [location-keyword-list]
  (map #(leaf-value %) location-keyword-list))

(defn translate-spatial-keywords
  "Translates a list of spatial keywords into an array of LocationKeyword type objects"
  [context spatial-keywords]
  (let [spatial-keyword-maps (get-spatial-keywords-maps context)
        location-keyword-maps (spatial-keywords->location-keywords spatial-keyword-maps spatial-keywords)
        umm-location-keyword-maps (seq
                                   (map
                                    #(dissoc
                                      (set/rename-keys % cache-location-keywords->umm-location-keywords)
                                      :uuid)
                                    location-keyword-maps))]
    umm-location-keyword-maps))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(defn xml-elem->location-keyword
  "Return a location keyword from a location element."
  [location-elem]
  (let [category (cx/string-at-path location-elem [:Location_Category])
        type (cx/string-at-path location-elem [:Location_Type])
        subregion-1 (cx/string-at-path location-elem [:Location_Subregion1])
        subregion-2 (cx/string-at-path location-elem [:Location_Subregion2])
        subregion-3 (cx/string-at-path location-elem [:Location_Subregion3])
        detailed-location (cx/string-at-path location-elem [:Detailed_Location])
        location-keyword
        {:Category category
         :Type type
         :Subregion1 subregion-1
         :Subregion2 subregion-2
         :Subregion3 subregion-3
         :DetailedLocation detailed-location}]
    location-keyword))

(defn xml-elem->spatial-keywords
  "Returns spatial keywords from all of the Location elements within a DIF9 or DIF10 collection."
  [collection-element]
  (seq
   (location-keywords->spatial-keywords
    (map xml-elem->location-keyword
         (cx/elements-at-path collection-element [:Location])))))

(defn generate-locations
  "Generate DIF9 or DIF10 locations based on UMM spatial keywords. Note this requires a context
  which umm-lib is not setup for yet, so this function is not used yet."
  [context spatial-keywords]
  (when-let [location-keywords (seq (translate-spatial-keywords context spatial-keywords))]
    (for [location location-keywords]
      (let [{:keys [category type subregion-1 subregion-2 subregion-3 detailed-location]} location]
        (x/element :Location {}
                   (x/element :Location_Category {} category)
                   (gu/optional-elem :Location_Type {} type)
                   (gu/optional-elem :Location_Subregion1 subregion-1)
                   (gu/optional-elem :Location_Subregion2 subregion-2)
                   (gu/optional-elem :Location_Subregion3 subregion-3)
                   (gu/optional-elem :Detailed_Location detailed-location))))))
